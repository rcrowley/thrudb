AC_PREREQ(2.59)

AC_INIT(thrucene, 1.0)

AM_INIT_AUTOMAKE

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC

AC_PROG_INSTALL

AC_PATH_PROG(THRIFT, thrift, thrift, $PATH:/usr/local/bin:/usr/bin)

AC_CHECK_LIB(clucene, [open], [], [
        echo "Error! You need to install clucene."
        exit -1
        ])


################################################################
## Crypto
###
AC_CHECK_LIB([ssl],[ssl3_new],,
	AC_MSG_ERROR([OpenSSL developer library 'libssl' not installed; cannot continue.]))
AC_CHECK_LIB([crypto],[MD5_Update],,
	AC_MSG_ERROR([OpenSSL developer library 'libcrypto' not installed; cannot continue.]))	

AC_CHECK_HEADERS([openssl/rand.h])
AC_CHECK_HEADERS([openssl/md5.h])
AC_CHECK_HEADERS([openssl/sha.h])
AC_CHECK_HEADERS([openssl/hmac.h])
AC_CHECK_HEADERS([openssl/fips_sha.h])
AC_CHECK_HEADERS([openssl/aes.h])
AC_CHECK_FUNCS([SHA256_Init])
AC_CHECK_FUNCS([AES_encrypt])
AC_CHECK_FUNCS([RAND_pseudo_bytes])


################################################################
## Amazon S3
## S3 requires curl and expat; otherwise we don't need them
AC_ARG_ENABLE(s3,
	AC_HELP_STRING([--enable-s3=yes],
	       [Support for Amazon's S3 service. Requires CURL and Expat. (default yes)]),
        [enable_s3=$enableval], [enable_s3=yes])

if test "x${enable_s3}" = "xyes" ; then
  AC_MSG_NOTICE([S3 support requested. Looking for curl and expat...])
  AC_PATH_PROG(CURL_CONFIG,curl-config)
  AC_ARG_WITH(curl,
            AC_HELP_STRING([--with-curl=PATH], [where libcurl is installed]),
            [CURL_CONFIG="${with_curl}/bin/curl-config"])
  if test -f "${CURL_CONFIG}"; then
      LDFLAGS="`${CURL_CONFIG} --libs` $LDFLAGS"
      CPPFLAGS="`${CURL_CONFIG} --cflags` $CPPFLAGS"
  else
      AC_MSG_WARN([curl-config not found, guessing at libcurl build settings])
  fi
  AC_CHECK_HEADER([curl/curl.h],,
    AC_MSG_WARN([curl/curl.h not found; Disabling S3 Support.])
    enable_s3=no)
  AC_CHECK_LIB([curl],[curl_global_init],,
    AC_MSG_WARN([Curl library corrupt; Disabling S3 Support.])
    enable_s3=no)
  AC_CHECK_HEADER([expat.h])
  AC_CHECK_LIB([expat],[XML_ParserCreate],, 
    AC_MSG_WARN([expat.h not found; Disabling S3 Support.])
    enable_s3=no)
fi

S3_BIN=
if test "${enable_s3}" = "yes" ; then
  AC_DEFINE([USE_S3],1,[Enable support for Amazon S3])
  S3_BIN='s3$(EXEEXT)'
fi
AC_SUBST(S3_BIN)

AC_OUTPUT(Makefile)

